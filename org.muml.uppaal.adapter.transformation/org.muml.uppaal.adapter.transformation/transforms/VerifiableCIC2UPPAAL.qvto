import transforms.rtsc.RTSCHierarchyNormalization;
import transforms.rtsc.RTSCDeadlineTransitionNormalization;
import transforms.rtsc.RTSCCompositeTransitionNormalization;
import transforms.rtsc.RTSCEntryExitEventNormalization;
import transforms.rtsc.RTSCScaleTimeValuesNormalization;
import transforms.rtsc.RTSCDoEventNormalization;
import transforms.rtsc.RTSCUrgencyNormalization;
import transforms.NameNormalization;
import transforms.cic.Protocol2CIC;
import transforms.MUML2UPPAAL;

import org.muml.uppaal.adapter._blackbox.ProgressUnit;
import org.muml.common._blackbox.SaveXMIBlackbox;

modeltype muml uses 'http://www.muml.org/pim/1.0.0';
modeltype uppaal uses 'http://www.muml.org/uppaal/1.0.0';
modeltype uppaal_requirements uses 'http://www.muml.org/uppaal/requirements/1.0.0';
modeltype verification_options uses 'http://www.muml.org/uppaal/options/1.0.0';


/*
* Transforms a property-normalized CIC to UPPAAL
*/

transformation VerifiableCIC2UPPAAL(in verifiable_cic : muml, in options : verification_options, out output_uppaal : uppaal, out output_req: uppaal_requirements);

configuration property STORE_INTERMEDIATE_MODELS : Boolean;

helper storeModel(model : Bag(Element), filename : String) {
	if (STORE_INTERMEDIATE_MODELS) then {
		save(model, "Intermediate Models", filename);
	} endif;
}

main() {
	storeModel(verifiable_cic.rootObjects()->asBag(), "1_after_CIC.muml");
	
	
	// Normalize time units used in the model (to one common time unit)
	reportProgress("Normalization: Time Units");
	var scaleClockValuesNormalizedModel : muml = muml.createEmptyModel().oclAsType(muml);
	var scaleClockValuesNormalization : RTSCScaleTimeValuesNormalization = new RTSCScaleTimeValuesNormalization(verifiable_cic, scaleClockValuesNormalizedModel);
	var retcode := scaleClockValuesNormalization.transform();
	storeModel(scaleClockValuesNormalizedModel.rootObjects()->asBag(), "2_after_TimeUnitNorm.muml");
	assert fatal (retcode.succeeded()) with log('Scale-Time-Values normalization failed: ' + retcode.raisedException().repr());
	
	//add muml prefix to all named elements and substitute unsupported substrings of the named elements' names
	reportProgress("Normalization: Set Qualifier");
	var prefixedModel : muml = muml.createEmptyModel().oclAsType(muml);
	var nameNormalization : NameNormalization = new NameNormalization(scaleClockValuesNormalizedModel, prefixedModel);
	retcode := nameNormalization.transform();
	storeModel(prefixedModel.rootObjects()->asBag(), "3_after_NameNorm.muml");
	assert fatal (retcode.succeeded()) with log('NamePrefix normalization failed: ' + retcode.raisedException().repr());

	// Normalize transitions with deadlines by adding an intermediate state
	reportProgress("Normalization: Deadlines");
	var transitionNormalizedModel : muml = muml.createEmptyModel().oclAsType(muml);
	var transitionDeadlineNormalization : RTSCDeadlineTransitionNormalization = new RTSCDeadlineTransitionNormalization(prefixedModel,transitionNormalizedModel);
	retcode := transitionDeadlineNormalization.transform();
	storeModel(transitionNormalizedModel.rootObjects()->asBag(), "4_after_DeadlineNorm.muml");
	assert fatal (retcode.succeeded()) with log('Deadline transition normalization failed: ' + retcode.raisedException().repr());
	
	// Replace composite transitions by equivalent transitions and exit/entry points
	reportProgress("Normalization: Composite Transitions");
	var compositeNormalizedModel : muml = muml.createEmptyModel().oclAsType(muml);
	var compositeNormalization : RTSCCompositeTransitionNormalization = new RTSCCompositeTransitionNormalization(transitionNormalizedModel,compositeNormalizedModel);
	retcode := compositeNormalization.transform();
	storeModel(compositeNormalizedModel.rootObjects()->asBag(), "5_after_CompositeTransitionNorm.muml");
	assert fatal (retcode.succeeded()) with log('Composite transition normalization failed: ' + retcode.raisedException().repr());
	
	// Replace do events by equivalent regions
	reportProgress("Normalization: State Do-Effect");
	var doEventNormalizedModel : muml = muml.createEmptyModel().oclAsType(muml);
	var doEventNormalization : RTSCDoEventNormalization = new RTSCDoEventNormalization(compositeNormalizedModel,doEventNormalizedModel);
	retcode := doEventNormalization.transform();
	storeModel(doEventNormalizedModel.rootObjects()->asBag(), "6_after_DoEventNorm.muml");
	assert fatal (retcode.succeeded()) with log('Do event normalization failed: ' + retcode.raisedException().repr());
		
	// Flatten the RTSCs into one equivalent flat RTSC with multiple regions without hierarchical states
	reportProgress("Normalization: Hierarchy Flattening");
	var flatModel : muml = muml.createEmptyModel().oclAsType(muml);
	var flattening : RTSCHierarchyNormalization = new RTSCHierarchyNormalization(doEventNormalizedModel,flatModel);
	retcode := flattening.transform();
	storeModel(flatModel.rootObjects()->asBag(), "7_after_HierarchyNorm.muml");	
	assert fatal (retcode.succeeded()) with log('Hierarchy normalization failed: ' + retcode.raisedException().repr());
	
	// Move entry/exit events of a state to incoming/outgoing transitions
	reportProgress("Normalization: State Entry-/Exit-Effect");
	var entryExitNormalizedModel : muml = muml.createEmptyModel().oclAsType(muml);
	var entryExitNormalization : RTSCEntryExitEventNormalization = new RTSCEntryExitEventNormalization(flatModel,entryExitNormalizedModel);
	retcode := entryExitNormalization.transform();
	storeModel(entryExitNormalizedModel.rootObjects()->asBag(), "8_after_StateEntryExitNorm.muml");	
	assert fatal (retcode.succeeded()) with log('Entry-Exit-Event normalization failed: ' + retcode.raisedException().repr());
	
	// Ensure that all transitions synchronizing over the same channel are either all urgent or all non-urgent.
	reportProgress("Normalization: Urgency of Transitions");
	var urgencyNormalizedModel : muml = muml.createEmptyModel().oclAsType(muml);
	var urgencyNormalization : RTSCUrgencyNormalization = new RTSCUrgencyNormalization(entryExitNormalizedModel,urgencyNormalizedModel);
	retcode := urgencyNormalization.transform();
	storeModel(urgencyNormalizedModel.rootObjects()->asBag(), "9_after_UrgencyNorm.muml");	
	assert fatal (retcode.succeeded()) with log('Urgency normalization failed: ' + retcode.raisedException().repr());
	
	// Migration to Uppaal, yielding the NTA model and the Uppaal "requirements" model
	reportProgress("Migration");
	var uppaalTransformation : MUML2UPPAAL = new MUML2UPPAAL(verifiable_cic, urgencyNormalizedModel, options, output_uppaal, output_req);
	retcode := uppaalTransformation.transform();
	assert fatal (retcode.succeeded()) with log('UPPAAL transformation failed: ' + retcode.raisedException().repr());		

}
