import transforms.EntryPoints;
import transforms.ExitPoints;
import transforms.util;
import transforms.selectorExpression;

modeltype ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');

modeltype transform uses transform('http://www.muml.de/modelica/transform/1.0.0');

modeltype instance uses pim::instance("http://www.muml.org/pim/1.0.0");
modeltype connector uses pim::connector("http://www.muml.org/pim/1.0.0");
modeltype component uses pim::component("http://www.muml.org/pim/1.0.0");
modeltype rtsctype uses pim::realtimestatechart("http://www.muml.org/pim/1.0.0");

// TODO: rewrite this mess
transformation RegionInstantiation(inout source : instance, out instanceMap : transform)
	access EntryPoints, ExitPoints, Util, SelectorExpression;

intermediate property RealtimeStatechart::origRTSC : RealtimeStatechart;

main() {
	map start();
}

mapping start() : Map {
	source.objects()[AtomicComponentInstance]->select(componentType.componentKind <> ComponentKind::CONTINUOUS_COMPONENT)->forEach(comp) {
		var rtsc : RealtimeStatechart := getRTSC(comp);
		rtsc.translateExitPoints();
		rtsc.translateEntryPoints();
		assert fatal (rtsc.embeddedStatecharts()->collect(states)->collect(connectionPoints)->isEmpty()) with log("prev trans failed");
		var clone : RealtimeStatechart = cloneRTSC(rtsc);
		entries += object Entry {
			key := comp;
			value := clone;
		};
		clone.map transform(comp);
		clone.map fixupRegions();
		--clone.allSubobjectsOfType(RegionInstance)[RegionInstance]->forEach(ri) {
			--log(ri.name + " single port: " + ri.embedsSinglePortStatechart.toString());
			--log(ri.name + " multi port: " + ri.embedsMultiPortStatechart.toString());
			--log(ri.name + " instanceOf: " + ri.instanceOf.embeddedStatechart.behavioralElement.toString());
		--};
	}
}

mapping in RealtimeStatechart::fixupRegions() {
	self.allSubobjectsOfType(Region)[Region]->forEach(region) {
		region.map transform();
	}
}

mapping inout Region::transform() {
	var state : State = self.parentState;
	state.embeddedRegions := state.embeddedRegions->select(r : Region | r <> self);
	var regionInstance = object RegionInstance {
		priority := self.priority;
		parentState := state;
		// do _not_ use a cloneRTSC because otherwise we lose
		// incomingTransitions if a there is a transition from a
		// parentState's entry point to a state which is contained
		// in self.embeddedStatechart
		// drawback: instanceOf.embeddedStatechart is null
		embeddedStatechart := self.embeddedStatechart;
		instanceOf := self;
	};
}

mapping in RealtimeStatechart::transform(in componentInstance : AtomicComponentInstance) {
	var state : State = self.states->first();
	state.map removePortRegions();
	state.map addPortRegionInstances(componentInstance);
}

mapping inout State::removePortRegions() {
	self.embeddedRegions := getNoPortRegions(self);
}

mapping inout State::addPortRegionInstances(in componentInstance : ComponentInstance) {
	// TODO: keep region order
	componentInstance.portInstances[DiscreteMultiPortInstance]->forEach(p) {
		var origRTSC : RealtimeStatechart = getRTSC(p);
		var regionInstance : RegionInstance = new RegionInstance(origRTSC, self, "");
		self.embeddedRegions += regionInstance;
		regionInstance.embeddedStatechart.map addSubPortRegionInstances(p.subInteractionEndpointInstances);
	};
	componentInstance.portInstances[DiscreteSinglePortInstance]->select(multiInteractionEndpointInstance.oclIsUndefined())->forEach(p) {
		var origRTSC : RealtimeStatechart = getRTSC(p);
		var regionInstance : RegionInstance = new RegionInstance(origRTSC, self, p.name);
		self.embeddedRegions += regionInstance;
	};
}

mapping inout RealtimeStatechart::addSubPortRegionInstances(in subInteractionEndpointInstances : OrderedSet(DiscreteSingleInteractionEndpointInstance)) {
	var state : State = self.states->first();
	var names : Set(String) = subInteractionEndpointInstances->collect(
		type.oclAsType(DiscretePort).behavior.oclAsType(RealtimeStatechart).name
	)->asSet();
	var priority : Integer = 1;
	var nonPortRegions : Set(Region) := state.embeddedRegions->select(not names->includes(name));
	// is there a more clever way to clear a list?
	state.embeddedRegions := state.embeddedRegions->select(false);
	var size : Integer := subInteractionEndpointInstances->size();
	// this is a ridiculous hack... which might even fail for some models
	// (implicit assumption: prio(adapt) > prio(port_region))
	subInteractionEndpointInstances->forEach(sub) {
		var regionInstance : RegionInstance := new RegionInstance(getRTSC(sub), state, sub.name, priority); 
		state.embeddedRegions += regionInstance;
		normalizeDiscreteInteractionEndpointReferences(self, regionInstance.embeddedStatechart, size);
		priority := priority + 1;
	};
	nonPortRegions->forEach(r) {
		// directly create a RegionInstance because the statechart might be
		// modified by a qvto transformation
		var regionInstance : RegionInstance := new RegionInstance(r.embeddedStatechart, state, r.embeddedStatechart.name, priority); 
		state.embeddedRegions += regionInstance;
		normalizeDiscreteInteractionEndpointReferences(self, regionInstance.embeddedStatechart, size);
		priority := priority + 1;
	};
}

helper cloneRTSC(rtsc : RealtimeStatechart, name : String) : RealtimeStatechart {
	var nrtsc : RealtimeStatechart = rtsc.deepclone().oclAsType(RealtimeStatechart);
	//nrtsc.name := name + nrtsc.name;
	if (name.length() > 0) then {
		nrtsc.name := name;
	}
	endif;
	nrtsc.origRTSC := rtsc;
	return nrtsc;
}

helper cloneRTSC(rtsc : RealtimeStatechart) : RealtimeStatechart {
	return cloneRTSC(rtsc, "");
}

query getNoPortRegions(state : State) : OrderedSet(Region) {
	var origState : State = state.parentStatechart.origRTSC.states->first();
	return 
	let noPortRegionNames : Set(String) = origState.embeddedRegions->select(
		not isPortStatechart(embeddedStatechart)
	)->collect(name)->asSet()
	in
	state.embeddedRegions->select(noPortRegionNames->includes(name));
}

query isPortStatechart(rtsc : RealtimeStatechart) : Boolean {
	var state : State = rtsc.states->first();
	return rtsc.states->size() = 1
		and state.embeddedRegions->select(
			not embeddedStatechart.behavioralElement.oclIsUndefined()
		)->notEmpty() or not rtsc.behavioralElement.oclIsUndefined();
}

query getRTSC(componentInstance : AtomicComponentInstance) : RealtimeStatechart {
	return componentInstance.componentType.oclAsType(AtomicComponent)
		.behavior.oclAsType(RealtimeStatechart);
}

query getRTSC(discreteSingleInteractionEndpointInstance : DiscreteSingleInteractionEndpointInstance)  : RealtimeStatechart{
	return discreteSingleInteractionEndpointInstance.type.oclAsType(DiscretePort)
		.behavior.oclAsType(RealtimeStatechart)
}

query getRTSC(discreteMultiPortInstance : DiscreteMultiPortInstance) : RealtimeStatechart {
	return discreteMultiPortInstance.portType.oclAsType(DiscretePort)
		.behavior.oclAsType(RealtimeStatechart);
}

constructor RegionInstance::RegionInstance(origRTSC : RealtimeStatechart, state : State, subPortName : String) {
	// get rid of this duplicate
	priority := origRTSC.parentRegion.priority;
	parentState := state;
	embeddedStatechart := cloneRTSC(origRTSC, subPortName);	
	instanceOf := origRTSC.parentRegion;
}

constructor RegionInstance::RegionInstance(origRTSC : RealtimeStatechart, state : State, subPortName : String, prio : Integer) {
	priority := prio;
	parentState := state;
	embeddedStatechart := cloneRTSC(origRTSC, subPortName);	
	instanceOf := origRTSC.parentRegion;
}