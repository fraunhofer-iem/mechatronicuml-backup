import 'Lookup.ecore'
import 'Pivot.ecore'

package ocl

context OclElement

def: parentEnv() : lookup::Environment =
	let parent = oclContainer() in if parent = null then lookup::Environment { } else parent._env(self) endif

def : _env(child : OclElement) : lookup::Environment =
	parentEnv()
	
def : lookupVariables(vName : String) : OrderedSet(Variable) =
	self._env(null).namedElements->selectByKind(Variable)->select(name = vName)
endpackage

package lookup

context Environment

def : addElementsOf(element : ocl::OclElement) : Environment =
	let newEnv = element._env(null)
	in Environment {
		namedElements = self.namedElements->including(newEnv.namedElements)
	}

def : addElementsOf(elements : Collection(ocl::OclElement)) : Environment =
	elements->iterate(element ; acc : Environment = self 
		| acc.addElementsOf(element))

endpackage

package pivot
		
context Class
def : _env(child : Element) : lookup::Environment =
	let superClasses : Set(Class) = self->closure(superClasses) in 
	parentEnv().nestedEnv()
		-- .addElements(self.ownedTemplateSignature.ownedParameter.parameteredElement->selectByKind(Type)) FIXME badOclOperation ???
		.addElements(superClasses.ownedProperties->select(not isStatic))
		.addElements(superClasses.ownedOperations->select(not isStatic))
		.addElements(superClasses.ownedBehaviors)

context DataType
def : _env(child : Element) : lookup::Environment =
	let superClasses : Set(Class) = self->closure(c : Class | c.superClasses->asSet()) in 
	parentEnv().nestedEnv()
		.addElements(superClasses.ownedProperties->select(not isStatic))
		.addElements(superClasses.ownedOperations->select(not isStatic))
		-- .addElements(self.ownedTemplateSignature.ownedParameter.parameteredElement->selectByKind(Type)) FIXME badOclOperation ???

context Element
def: _env(child : Element) : lookup::Environment =
	parentEnv()
	
context Enumeration
def : _env(child : Element) : lookup::Environment =
	parentEnv().nestedEnv()
		-- .addElements(self.ownedTemplateSignature.ownedParameter.parameteredElement->selectByKind(Type)) FIXME badOclOperation ???
		.addElements(self.ownedLiterals)
		.addElements(self.ownedProperties->select(not isStatic))
		.addElements(self.ownedOperations->select(not isStatic))
		.addElements(self.ownedBehaviors)
		


context ExpressionInOCL
def : _env(child : Element) : lookup::Environment =
	parentEnv().nestedEnv()
		.addElement(self.ownedContext)
		.addElements(self.ownedParameters)
		.addElement(self.ownedResult)

context IterateExp
def : _env(child : Element) : lookup::Environment =
	if child = ownedResult
	    then parentEnv().nestedEnv().addElements(self.ownedIterators)
	    else let index = ownedIterators->indexOf(child) in
		    if index > 1 
		        then parentEnv().nestedEnv().addElements(self.ownedIterators->subOrderedSet(1, index-1))
		        else parentEnv().nestedEnv().addElements(self.ownedIterators).addElement(ownedResult)
		    endif
	endif

context IteratorExp
def : _env(child : Element) : lookup::Environment =
	let index = ownedIterators->indexOf(child) in
	if index > 1 
        then parentEnv().nestedEnv().addElements(self.ownedIterators->subOrderedSet(1, index-1))
        else parentEnv().nestedEnv().addElements(self.ownedIterators)
    endif

context LetExp
def : _env(child : Element) : lookup::Environment =
	if child = ownedIn
		then parentEnv().nestedEnv().addElement(self.ownedVariable)
		else parentEnv()
	endif

context Library
def : _env(child : Element) : lookup::Environment =
	parentEnv().nestedEnv()
		.addElements(self.ownedPackages)
		.addElements(self.ownedClasses)
		.addElements(self.ownedPrecedences)

context Operation
def : _env(child : Element) : lookup::Environment =
	if ownedParameters->includes(child)
		then parentEnv().nestedEnv()
		 	-- .addElements(self.ownedTemplateSignature.ownedParameter.parameteredElement->selectByKind(Type)) FIXME badOclOperation ???
		else parentEnv().nestedEnv()
			.addElements(self.ownedParameters)
			-- .addElements(self.ownedTemplateSignature.ownedParameter.parameteredElement->selectByKind(Type)) FIXME badOclOperation ???
	endif

context Package
def : _env(child : Element) : lookup::Environment =
	parentEnv().nestedEnv()
		.addElements(self.ownedPackages)
		.addElements(self.ownedClasses)

context Model
def : _env(child : Element) : lookup::Environment =
	parentEnv()
		.addElements(self.ownedImports)
		.addElements(self.ownedPackages)

endpackage