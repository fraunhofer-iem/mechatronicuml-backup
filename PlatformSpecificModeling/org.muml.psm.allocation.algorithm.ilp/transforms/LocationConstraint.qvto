import Expressions;
import HWUtil;
import OCLEvaluator;
import Util;
import VariableHelper;
import Variables;

modeltype allocationSpecification uses cs('http://www.muml.org/psm/allocation/language/cs/1.0.0');
modeltype oclcontext uses oclcontext('http://www.muml.org/psm/allocation/language/oclcontext/1.0.0');
modeltype core uses core('http://www.muml.org/core/1.0.0');
modeltype ilptype uses ilp('http://www.muml.org/psm/allocation/ilp/1.0.0');
modeltype expressions uses core::expressions('http://www.muml.org/core/expressions/1.0.0');
modeltype common uses core::expressions::common('http://www.muml.org/core/expressions/common/1.0.0');
modeltype instance uses pim::instance('http://www.muml.org/pim/instance/1.0.0');
modeltype hwpinstance uses hardware::hwplatforminstance('http://www.muml.org/pm/hardware/hwplatforminstance/1.0.0');
modeltype hwrinstance uses hardware::hwresourceinstance('http://www.muml.org/pm/hardware/hwresourceinstance/1.0.0');
modeltype values uses values('http://www.eclipse.org/ocl/2015/Values');


library LocationConstraint;

// the implementation of the differentLocation constraint differs from the approach,
// which was described in my thesis

helper SpecificationCS::createLocationConstraints(inout ilp : IntegerLinearProgram,
	cic : ComponentInstanceConfiguration,
	hpic : HWPlatformInstanceConfiguration) {
	var startTime : Integer;
	var endTime : Integer;
	startTime := getCurrentTime();
	self.getSameLocationConstraints()->createLocationConstraint(ilp, cic, hpic);
	endTime := getCurrentTime();
	log("Time for create collocation Constraints:"+((endTime-startTime)/1000).toString()+" seconds");
	startTime := getCurrentTime();
	self.getDifferentLocationConstraints()->createLocationConstraint(ilp, cic, hpic);
	endTime := getCurrentTime();
	log("Time for create separate location Constraints:"+((endTime-startTime)/1000).toString()+" seconds");
}

helper LocationConstraintCS::createLocationConstraint(inout ilp : IntegerLinearProgram,
	cic : ComponentInstanceConfiguration,
	hpic : HWPlatformInstanceConfiguration) {
	var sequences : Sequence(Sequence(NamedElement)) := self.getHelperVariableSequences(cic, hpic);
	sequences->createHelperVariablesAndConstraints(ilp);
	var instances : OrderedSet(NamedElement) := sequences->collect(seq : Sequence(NamedElement) |
		seq->at(1)
	)->asOrderedSet()->sortedBy(name);
	ilp.constraints += instances->createLocationConstraintConstraintExpression(sequences, self);
}

helper NamedElement::createLocationConstraintConstraintExpression(sequences : Sequence(Sequence(NamedElement)),
	locationConstraint : LocationConstraintCS) : ConstraintExpression {
	var mySequences : Sequence(Sequence(NamedElement)) := self.getSequences(sequences)->asSequence();
	var comment : String := if locationConstraint.name <> '' then locationConstraint.name else locationConstraint.repr() endif;
	var lit : String := if locationConstraint.type = LocationConstraintTypes::SAME_LOCATION then
			mySequences->collect(seq : Sequence(NamedElement) | seq->at(3))
				->asSet()->size().toString()
		else
			'0'
		endif;
	return mySequences->createAdditionConstraintExpression(lit, comment);
}

// returns "my" sequences
query NamedElement::getSequences(sequences : Sequence(Sequence(NamedElement))) : OrderedSet(Sequence(NamedElement)) {
	return sequences->select(seq : Sequence(NamedElement) | seq->at(1) = self)
		->asOrderedSet()->sortedBy(getVariableName());
}

// removed

query LocationConstraintCS::getHelperVariableSequences(cic : ComponentInstanceConfiguration,
	hpic : HWPlatformInstanceConfiguration) : Sequence(Sequence(NamedElement)) {
	var oclContext : OCLContext := createOCLContext(cic, hpic);
	var res : Sequence(TupleValue) := self.evaluateOCL(oclContext);
	return res->getLocationConstraintHelperVariableSequences(hpic.getResourceInstances());
}

query Sequence(TupleValue)::getLocationConstraintHelperVariableSequences(resourceInstanceSet : OrderedSet(ResourceInstance))
	: Sequence(Sequence(NamedElement)) {
	return self->iterate(it : TupleValue; acc : Sequence(Sequence(NamedElement)) = Sequence{} |
		acc->union(it.getLocationConstraintHelperVariableSequences(resourceInstanceSet))
	)->sortedBy(getVariableName());
}

query TupleValue::getLocationConstraintHelperVariableSequences(resourceInstanceSet : OrderedSet(ResourceInstance))
	: Sequence(Sequence(NamedElement)) {
	// sort by instance set by variable name
	var sequence : Sequence(NamedElement) := self.toSequence()->sortedBy(getVariableName());
	var size : Integer = sequence->size();
	assert fatal (size = 2) with log("unexpected sequence size: " + size.toString());
	return resourceInstanceSet->sortedBy(getVariableName())->collectNested(ri : ResourceInstance |
		Sequence{sequence->at(1), ri, sequence->at(2), ri}
	);
}

mapping inout IntegerLinearProgram::createSameLocationConstraints(cic : ComponentInstanceConfiguration,
	hpic : HWPlatformInstanceConfiguration) {
}

query SpecificationCS::getLocationConstraints(wantedType : LocationConstraintTypes) : OrderedSet(LocationConstraintCS) {
	return self.constraints->select(oclIsKindOf(LocationConstraintCS))
		->collect(oclAsType(LocationConstraintCS))
		->select(type = wantedType)
		->asOrderedSet();	
}

query SpecificationCS::getSameLocationConstraints() : OrderedSet(LocationConstraintCS) {
	return self.getLocationConstraints(LocationConstraintTypes::SAME_LOCATION);
}

query SpecificationCS::getDifferentLocationConstraints() : OrderedSet(LocationConstraintCS) {
	return self.getLocationConstraints(LocationConstraintTypes::DIFFERENT_LOCATION);
}