transformation InstantiationTransformation(inout instance : InstancePackage);

modeltype MumlPackage uses muml('http://www.fujaba.de/muml/0.2.2');
modeltype ModelPackage uses muml::model('http://www.fujaba.de/muml/0.2.2');
modeltype ConnectorPackage uses muml::model::connector('http://www.fujaba.de/muml/0.2.2');
modeltype ValuetypePackage uses muml::model::valuetype('http://www.fujaba.de/muml/0.2.2');
modeltype ComponentPackage uses muml::model::component('http://www.fujaba.de/muml/0.2.2');
modeltype InstancePackage uses muml::model::instance('http://www.fujaba.de/muml/0.2.2');
modeltype Ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');

main() {
	log("QVT-O Script \"instances.qvto\" started.");

	// process AtomicComponentInstances
	instance.rootObjects()[AtomicComponentInstance]->forEach(ci) {
		ci.portInstances := ci.componentType.createPortInstances();
	};
	
	// process StructuredComponentInstances
	instance.rootObjects()[StructuredComponentInstance]->forEach(ci) {
		ci.portInstances := ci.componentType.createPortInstances();
		ci.embeddedCIC := new ComponentInstanceConfiguration(ci.componentType.oclAsType(StructuredComponent));
	};
}

// Queries
query NaturalNumber::toInteger() : Integer {
	if (self.infinity) then {
		return -1;
	} endif;
	return self.value;
}
query Component::createPortInstances() : Set(PortInstance) {
	var portInstances : Set(PortInstance);
	self.ports->forEach(port) {
		portInstances += port.createPortInstances();
	};
	return portInstances;
}
query Port::createPortInstances() : Set(PortInstance) {
	return Set { self.createPortInstance() }
}
query DiscretePort::createPortInstances() : Set(PortInstance) {
	if (self.multiPort) then {
		var multiPortInstance : DiscreteMultiPortInstance := new DiscreteMultiPortInstance(self);
		//return OrderedSet { multiPortInstance }
		return OrderedSet { multiPortInstance }[PortInstance]->union(multiPortInstance.subInteractionEndpointInstances[PortInstance])
	} endif;
	return self.createSubPortInstances();
}
query DiscretePort::createSubPortInstances() : Set(PortInstance) {
	var subPortInstances : Set(PortInstance);
	var count : Integer := 0;
	while (count < self.cardinality.lowerBound.toInteger()) {
		var portInstance : PortInstance := self.createPortInstance();
		portInstance.name := self.name + (count + 1).toString();
		subPortInstances += portInstance;
		count := count + 1;
	};
	return subPortInstances
}

query Port::createPortInstance() : PortInstance {
	return null;
}
query DiscretePort::createPortInstance() : PortInstance {
	return new DiscreteSinglePortInstance(self);
}
query ContinuousPort::createPortInstance() : PortInstance {
	return new ContinuousPortInstance(self);
}
query HybridPort::createPortInstance() : PortInstance {
	return new HybridPortInstance(self);
}


// Constructors
constructor ComponentInstanceConfiguration::ComponentInstanceConfiguration(component : StructuredComponent) {
	name := component.name;
	component.embeddedParts.componentType[AtomicComponent]->forEach(s) {
		componentInstances += new AtomicComponentInstance(s);
	};
	component.embeddedParts.componentType[StructuredComponent]->forEach(s) {
		componentInstances += new StructuredComponentInstance(s);
	};
}
constructor AtomicComponentInstance::AtomicComponentInstance(type : AtomicComponent) {
	componentType := type;
	name := type.name;
	portInstances := type.createPortInstances();
}
constructor StructuredComponentInstance::StructuredComponentInstance(type : StructuredComponent) {
	componentType := type;
	name := type.name;
	portInstances := type.createPortInstances();
	embeddedCIC := new ComponentInstanceConfiguration(type);
}
constructor DiscreteMultiPortInstance::DiscreteMultiPortInstance(port : DiscretePort) {
	type := port;
	name := type.name;
	subInteractionEndpointInstances := port.createSubPortInstances()[DiscreteSinglePortInstance];
}
constructor DiscreteSinglePortInstance::DiscreteSinglePortInstance(port : DiscretePort) {
	type := port;
}
constructor ContinuousPortInstance::ContinuousPortInstance(port : ContinuousPort) {
	type := port;
}
constructor HybridPortInstance::HybridPortInstance(port : HybridPort) {
	type := port;
}