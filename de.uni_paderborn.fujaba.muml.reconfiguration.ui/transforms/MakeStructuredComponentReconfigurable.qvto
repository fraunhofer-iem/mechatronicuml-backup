
--core models
modeltype core uses core('http://www.storydriven.org/core/0.3.1');
modeltype expressions uses core::expressions('http://www.storydriven.org/core/0.3.1');
modeltype expressions_common uses core::expressions::common('http://www.storydriven.org/core/0.3.1');
modeltype modelinst uses modelinstance("http://www.fujaba.de/modelinstance/0.3.15");

-- muml models 
modeltype behavior uses muml::behavior("http://www.fujaba.de/muml/0.2.2");
modeltype component uses muml::component("http://www.fujaba.de/muml/0.2.2");
modeltype connector uses muml::connector("http://www.fujaba.de/muml/0.2.2");
modeltype constraints uses muml::constraint("http://www.fujaba.de/muml/0.2.2");
modeltype deployment uses muml::deployment("http://www.fujaba.de/muml/0.2.2");
modeltype instance uses muml::instance("http://www.fujaba.de/muml/0.2.2");
modeltype msgi uses muml::msgtype("http://www.fujaba.de/muml/0.2.2");
modeltype protocol uses muml::protocol("http://www.fujaba.de/muml/0.2.2");
modeltype realtimestatechart uses muml::realtimestatechart('http://www.fujaba.de/muml/0.2.2');
modeltype types uses muml::types("http://www.fujaba.de/muml/0.2.2");
modeltype valuetype uses muml::valuetype("http://www.fujaba.de/muml/0.2.2");

modeltype actionlanguage uses actionlanguage('http://www.fujaba.de/muml/actionlanguage/0.3.16');
modeltype reconf uses reconfiguration("http://www.fujaba.de/muml/reconfiguration/0.3.18");
modeltype reconf_expressions uses reconfiguration::expression("http://www.fujaba.de/muml/reconfiguration/0.3.18");
modeltype reconf_datatype uses reconfiguration::structdatatype("http://www.fujaba.de/muml/reconfiguration/0.3.18");

modeltype componentpattern uses componentstorydiagram::componentstorypattern("http://www.fujaba.de/muml/componentstorydiagram/0.3.1");
modeltype componentstory uses componentstorydiagram::controlflow("http://www.fujaba.de/muml/componentstorydiagram/0.3.1");


-- this transformation creates a copy of the inputComponent, adds it to the category, and redirects all references
-- from the inputComponent to the copy
transformation MakeStructuredComponentReconfigurable(in inputComponent : component, inout category : modelinst);

main() {

	-- parse input parameters
	var oldComponent : StaticStructuredComponent := inputComponent.objects()[StaticStructuredComponent] -> any(true);
	var componentCategory : ModelElementCategory := category.objects()[ModelElementCategory] -> any(true);

	var newComponent : ReconfigurableStructuredComponent := map createReconfigurableComponent(oldComponent);
	
	componentCategory.modelElements += newComponent;
}


mapping createReconfigurableComponent(in oldComp : StaticStructuredComponent) : ReconfigurableStructuredComponent {
	
	-- copy attributes
	name := oldComp.name;						-- from NamedElement
	comment := oldComp.comment;					-- from CommentableElement
	componentType := oldComp.componentType;		-- from Component
	
	-- move features contained in references
	constraint := oldComp.constraint;			-- from ConstrainableElement
	ports := oldComp.ports; 					-- from Component
	connectors := oldComp.connectors;			-- from StructuredComponent
	coordinationProtocolOccurences := oldComp.coordinationProtocolOccurences; -- from StructuredComponent
	embeddedComponentParts := oldComp.embeddedComponentParts;	-- from StructuredComponent	
	
	-- create reconfiguration ports
	ports += new ReconfigurationMessagePort('parent', false); 
	ports += new ReconfigurationExecutionPort('parent', false);
	
	-- instantiate reconfiguration controllers and connections
	controller := new RuleBasedReconfigurationController();
	
	-- create assembly between manager and executor
	connectors += new ReconfigurationPortAssemblyConnector(controller.oclAsType(RuleBasedReconfigurationController).manager.ports 
			-> select(p : Port | p.oclIsTypeOf(InternalReconfigurationCommunicationPort)) -> any(true), controller.oclAsType(RuleBasedReconfigurationController).executor.ports 
			-> select(p : Port | p.oclIsTypeOf(InternalReconfigurationCommunicationPort)) -> any(true));
	
	-- create delegations
	connectors += new ReconfigurationPortDelegationConnector(controller.oclAsType(RuleBasedReconfigurationController).manager.ports 
			-> select(p : Port | p.oclIsTypeOf(ReconfigurationMessagePort) and p.name = 'parent') -> any(true), ports 
			-> select(p : Port | p.oclIsTypeOf(ReconfigurationMessagePort)) -> any(true));
			
	connectors += new ReconfigurationPortDelegationConnector(controller.oclAsType(RuleBasedReconfigurationController).executor.ports 
			-> select(p : Port | p.oclIsTypeOf(ReconfigurationExecutionPort) and p.name = 'parent') -> any(true), ports 
			-> select(p : Port | p.oclIsTypeOf(ReconfigurationExecutionPort)) -> any(true));
}

-- creates a rule based reconfiguration controller consisting of a manager and an executor for 
-- the reconfigurable structure component
constructor RuleBasedReconfigurationController::RuleBasedReconfigurationController(){
	
	-- create manager and executor including their ports
	manager := new Manager();
	executor := new Executor();
}

-- creates a manager for the rule based reconfiguration controller
constructor Manager::Manager(){
	ports += new InternalReconfigurationCommunicationPort('executor');
	ports += new ReconfigurationMessagePort('parent', false);
	ports += new ReconfigurationMessagePort('embeddedCI', true);
}

-- creates an executor for the rule based reconfiguration controller
constructor Executor::Executor(){
	ports += new InternalReconfigurationCommunicationPort('events');
	ports += new ReconfigurationExecutionPort('parent', false);
	ports += new ReconfigurationExecutionPort('embeddedCI', true);
}

-- creates a discrete port with the specified name
constructor InternalReconfigurationCommunicationPort::InternalReconfigurationCommunicationPort(in theName : String){
	name := theName;
	cardinality := new Cardinality(1,1);
}

-- creates a reconfiguration message port with the specified name
constructor ReconfigurationMessagePort::ReconfigurationMessagePort(in theName : String, in isMulti : Boolean){
	name := theName;
	
	if(isMulti = true) then{
		cardinality := new Cardinality(1, -1);
	} else {
		cardinality := new Cardinality(1, 1);
	}
	endif;
}

-- creates a reconfiguration execution port with the specified name
constructor ReconfigurationExecutionPort::ReconfigurationExecutionPort(in theName : String, in isMulti : Boolean){
	name := theName;
	
	if(isMulti = true) then{
		cardinality := new Cardinality(1, -1);
	} else {
		cardinality := new Cardinality(1, 1);
	}
	endif;
}

-- create a cardinality object with the given bounds, an uBound of -1 encodes infinity
constructor Cardinality::Cardinality(in lBound : Integer, in uBound : Integer){
	lowerBound := object NaturalNumber{
		value := lBound;
		infinity := false;
	};
	
	if(uBound = -1) then{
		upperBound := object NaturalNumber{
			value := lBound + 1;
			infinity = true;
		}
	} else {
		upperBound := object NaturalNumber{
			value := uBound;
			infinity = false;
		}
	} endif;
}

constructor ReconfigurationPortAssemblyConnector::ReconfigurationPortAssemblyConnector(in srcPort : Port, in tgtPort : Port){
	connectorEndpoints += srcPort;
	connectorEndpoints += tgtPort;
}

constructor ReconfigurationPortDelegationConnector::ReconfigurationPortDelegationConnector(in srcPort : Port, in tgtPort : Port){
	connectorEndpoints += srcPort;
	connectorEndpoints += tgtPort;
}