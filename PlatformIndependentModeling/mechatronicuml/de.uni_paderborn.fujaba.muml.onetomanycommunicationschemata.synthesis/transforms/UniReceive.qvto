library UniReceive;
import AllSchemataMappings;
modeltype ecore uses 'http://www.eclipse.org/emf/2002/Ecore';
modeltype rtsc uses muml::realtimestatechart('http://www.fujaba.de/muml/0.4.0');
modeltype common uses core::expressions::common('http://www.storydriven.org/core/0.3.1');
modeltype expressions uses core::expressions('http://www.storydriven.org/core/0.3.1');
modeltype behavior uses muml::behavior('http://www.fujaba.de/muml/0.4.0');
modeltype actionLanguage uses 'http://www.fujaba.de/muml/actionlanguage/0.4.0';
modeltype valuetype uses muml::valuetype('http://www.fujaba.de/muml/0.4.0');
modeltype protocol uses muml::protocol('http://www.fujaba.de/muml/0.4.0');
modeltype storydriven uses 'http://www.storydriven.org/core/0.3.1';
modeltype modelinstance uses modelinstance('http://www.fujaba.de/modelinstance/0.4.0');
modeltype schemata uses muml::realtimestatechart::one_to_n_schemata('http://www.fujaba.de/muml/0.4.0');
modeltype types uses muml::types('http://www.fujaba.de/muml/0.4.0');

----------------------------------------------------------XXXXXXXXXXXXXXXXX--SINGLE-RECEIVE--XXXXXXXXXXXXXXXXX-------------------------------------------------------------------------

--------------------------------------------------------------Transfer Schema SingleReceive of Coordinator------------------------------------------------------------------
	
mapping Transition :: SingleReceive2TransSendSyncCoordinator(context:OclAny, region:OclAny) : Transition {

		priority := self.priority;
		
		source := self.map SingleReceive2StateCoordinator(context, region);
		
		target := self.target.map Vertex2VertexCoordinator(context, region);
		
		clockResets += self.clockResets->select(c|not isClockSubRoleSpecific(c)).map Clock2Clock();
		
		action := self.action.map Action2Action(1, region);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::SEND, "first", "singleReceive", true, 1);
			
	}

mapping Transition :: SingleReceive2SourceRegionCoordinator(context:OclAny, region:OclAny) : Region {
		
		priority := self.priority;
		
		parentState := self.source.map Vertex2VertexCoordinator(context, region).oclAsType(State);
		
		embeddedStatechart := self.map SingleReceive2SourceEmbeddedRTSCCoordinator(context, region);
					
	}
	
mapping Transition :: SingleReceive2SourceEmbeddedRTSCCoordinator(context:OclAny, region:OclAny) : RealtimeStatechart {
		
		name:= "singleReceiveCheck" + "_" + self.source.name + "_" + self.target.name + "_" + self.priority.toString();
		
		states += self.map RegionState(context, region, "Idle", true);
		states += self.map RegionState(context, region, "Requesting", false);
		
		transitions += self.map SingleReceive2TransCheckEmbeddedCoordinator(context, region);
		transitions += self.map SingleReceive2TransCheckDoneEmbeddedCoordinator(context, region);
					
	}
	
mapping Transition :: SingleReceive2TransCheckEmbeddedCoordinator(context:OclAny, region:OclAny) : Transition {

		priority := 1;
		
		source := self.map RegionState(context, region, "Idle", true);
		
		target := self.map RegionState(context, region, "Requesting", false);
		
		clockConstraints := self.clockConstraints->select(c|not isClockSubRoleSpecific(c.clock))->map ClockConstraint2ClockConstraint(2, region);
		
		guard := self.guard.map Expression2Expression(context);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::SEND, "first", "singleReceiveCheck", true, 2);
			
	}
	
mapping Transition :: SingleReceive2TransCheckDoneEmbeddedCoordinator(context:OclAny, region:OclAny) : Transition {

		priority := 1;
		
		target := self.map RegionState(context, region, "Idle", true);
		
		source := self.map RegionState(context, region, "Requesting", false);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::RECEIVE, "", "singelReceiveCheckDone", false, 3);
		
		guard := self.map TransferSchemaGuard(context, region, "singleReceivePossible", true, 1);
			
	}
	
	
mapping Transition :: SingleReceive2StateCoordinator(context:OclAny, region:OclAny) : State {
	
		name := self.source.name + "_" + self.target.name + "_" + self.priority.toString() +"_Receiving";
		
		parentStatechart := self.statechart.map RTSC2CoordinatorRTSC(context, region);	
						
	}
	
mapping Transition :: SingleReceive2TransReceiveSyncCoordinator(context:OclAny, region:OclAny) : Transition {
	
		priority := self.priority;
		
		target := self.map SingleReceive2StateCoordinator(context, region);
		
		source := self.source.map Vertex2VertexCoordinator(context, region);		
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::RECEIVE, "", "singleReceiveCheckDone", false, 4);
		
		clockConstraints := self.clockConstraints->select(c|not isClockSubRoleSpecific(c.clock))->map ClockConstraint2ClockConstraint(1, region);
		
		if(self.guard.oclIsUndefined()){
			guard := self.map TransferSchemata2TypedNamedElementExpr(context, "singleReceivePossible",2);
		}else{
			guard := self.map TransferSchemaGuardAndPossibleGuard(context, 5, region, "singleReceivePossible", 2);
		}endif;
					
	}
	
--------------------------------------------------------------- Transfer Schema SingleReceive of Subrole------------------------------------------------------------------
	
mapping Transition :: SingleReceive2TransSendSyncFirstSubrole(context:OclAny, region:OclAny, toplevel:Boolean) : Transition {

		priority := (self.priority-1)*2+2;
		
		source := self.source.map Vertex2VertexSubrole(context, region);
		
		target := self.map SingleReceive2StateSubrole(context, region, toplevel);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::RECEIVE, "self", "singleReceive", true, 5);
		
		clockConstraints += self.clockConstraints->select(c|isClockSubRoleSpecific(c.clock)).map ClockConstraint2ClockConstraint(context, region);
		
		clockResets += self.clockResets->select(c|isClockSubRoleSpecific(c)).map Clock2Clock();	
		
		events += self.triggerMessageEvent.map Asynchronous2Asynchronous(context);
		
		guard := self.map TransferSchemaSingleSendAndPossibleGuard(context, 4, region,"consumeSingleReceive", "singleReceivePossible", false, 3);
		
		if(self.action.oclIsUndefined()){
			action := self.map TransferSchemata2Action(context, "consumeSingleReceive:=false", "consumeSingleReceive", "false", 1)
		}else{
			action := self.map TransferSchemata2TwoActionsTransAct(context, "consumeSingleReceive:=false", "consumeSingleReceive", "false", 1);
		}endif;
					
	}
	
mapping Transition :: SingleReceive2TransReceiveSyncNotLastSubrole(context:OclAny, region:OclAny, toplevel:Boolean) : Transition {
	
		priority := 2;
		
		target := self.target.map Vertex2VertexSubrole(context, region);
		
		source := self.map SingleReceive2StateSubrole(context, region, toplevel);		
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::SEND, "self.next", "singleReceive", true, 6);
		
		guard := self.map TransferSchemataLogicalExpression(context, region, "<>", "last", 4);
					
	}
			
mapping Transition :: SingleReceive2TransReceiveSyncLastSubrole(context:OclAny, region:OclAny, toplevel:Boolean) : Transition {
	
		priority := 1;
		
		target := self.target.map Vertex2VertexSubrole(context, region);
		
		source := self.map SingleReceive2StateSubrole(context, region, toplevel);		
		
		guard := self.map TransferSchemataLogicalExpression(context, region, "==", "last", 5);
				
		action := self.map TransferSchemata2Action(context, "singleReceivePossible:=false", "singleReceivePossible", "false", 12);
					
	}
	
mapping Transition :: SingleReceive2StateSubrole(context:OclAny, region:OclAny, toplevel:Boolean) : State {
	
		name := self.source.name + "_" + self.target.name + "_" + self.priority.toString() +"_Receiving";
		
		parentStatechart := self.statechart.map RTSC2SubroleRTSC(context, region);			
					
	}
	
mapping Transition :: SingleReceive2SourceRegionSubrole(context:OclAny, region:OclAny) : Region {
		
		priority := self.priority;
		
		parentState := self.source.map Vertex2VertexSubrole(context, region).oclAsType(State);
		
		embeddedStatechart := self.map SingleReceive2SourceEmbeddedRTSCSubrole(context, region);
					
	}
	
mapping Transition :: SingleReceive2SourceEmbeddedRTSCSubrole(context:OclAny, region:OclAny) : RealtimeStatechart {
		
		name:= "singleReceiveCheck" + "_" + self.source.name + "_" + self.target.name + "_" + self.priority.toString();
		
		states += self.map RegionState(context, region, "Idle", true);
		states += self.map RegionState(context, region, "Requested", false);
		
		transitions += self.map SingleReceive2RequestedIdleCheckSubrole(context, region);
		transitions += self.map SingleReceive2RequestedIdleCheckDoneLastSubrole(context, region);
		transitions += self.map SingleReceive2RequestedIdleCheckDonePossibleSubrole(context, region);
		transitions += self.map SingleReceive2IdleRequestedFulfilledSubrole(context, region);
		transitions += self.map SingleReceive2IdleRequestedNotFulfilledSubrole(context, region);
					
	}
	
mapping Transition :: SingleReceive2RequestedIdleCheckSubrole(context:OclAny, region:OclAny) : Transition {
	
		priority := 2;
				
		source := self.map RegionState(context, region, "Requested", false);
		
		target := self.map RegionState(context, region, "Idle", true);
		
		guard := self.map SingleReceiveNotLastAndPossibleExpression(context, region, "singleReceivePossible", true, 60);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::SEND, "self.next", "singleReceiveCheck", true, 7);
		
	}
	
mapping Transition :: SingleReceive2RequestedIdleCheckDoneLastSubrole(context:OclAny, region:OclAny) : Transition {
	
		priority := 1;
				
		source := self.map RegionState(context, region, "Requested", false);
		
		target := self.map RegionState(context, region, "Idle", true);
		
		guard := self.map TransferSchemataLogicalExpression(context, region, "==", "last", 7);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::SEND, "", "singleReceiveCheckDone", false, 8);
					
	}
	
mapping Transition :: SingleReceive2RequestedIdleCheckDonePossibleSubrole(context:OclAny, region:OclAny) : Transition {
	
		priority := 3;
				
		source := self.map RegionState(context, region, "Requested", false);
		
		target := self.map RegionState(context, region, "Idle", true);
		
		guard := self.map SingleReceiveNotLastAndPossibleExpression(context, region, "singleReceivePossible", false, 7);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::SEND, "", "singleReceiveCheckDone", false, 9);
		
	}
	
mapping Transition :: SingleReceive2IdleRequestedFulfilledSubrole(context:OclAny, region:OclAny) : Transition {
	
		priority := 2;
				
		target := self.map RegionState(context, region, "Requested", false);
		
		source := self.map RegionState(context, region, "Idle", true);
		
		if(not self.triggerMessageEvent.oneToManyCommunicationSchema.condition.oclIsUndefined()){
			guard := self.triggerMessageEvent.oneToManyCommunicationSchema.condition.map Expression2Expression(2);
		}endif;
			
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::RECEIVE, "self", "singleReceiveCheck", true, 10);
		
		action := self.map TransferSchemata2TwoActions(context, "singleReceivePossible:=true; consumeSingleReceive:=true", "singleReceivePossible", "consumeSingleReceive", "true", 10);
		
		events += self.triggerMessageEvent.map TriggerMessage2TriggerWithoutConsume(1);
							
	}
	
mapping Transition :: SingleReceive2IdleRequestedNotFulfilledSubrole(context:OclAny, region:OclAny) : Transition {
	
		priority := 1;
				
		target := self.map RegionState(context, region, "Requested", false);
		
		source := self.map RegionState(context, region, "Idle", true);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::RECEIVE, "self", "singleReceiveCheck", true, 11);
					
	}
	
mapping Transition :: SingleReceive2TransSendSyncNotConditionSubrole(context:OclAny, region:OclAny, toplevel:Boolean) : Transition {
		
		source := self.source.map Vertex2VertexSubrole(context, region);
		
		target := self.map SingleReceive2StateSubrole(context, region, toplevel);
		
		synchronization := self.map TransferSchemata2Synchronization(context, region, SynchronizationKind::RECEIVE, "self", "singleReceive", true, 12);--error
		
		clockResets += self.clockResets->select(c|isClockSubRoleSpecific(c)).map Clock2Clock();
				
		guard := self.map TransferSchemaSingleSendAndPossibleGuard(context, 4, region,"consumeSingleReceive", "singleReceivePossible", true, 11);
		
		priority := (self.priority-1)*2+1;
		
	}
	
mapping Transition :: SingleReceiveNotLastAndPossibleExpression(context:OclAny, region:OclAny, varName:String, notOp: Boolean, transMapping: Integer) : LogicalExpression {

		leftExpression := self.map TransferSchemataLogicalExpression(context, region, "<>", "last", 10+transMapping);
		
		if(notOp){
			rightExpression := self.map TransferSchemaGuard(context, region, varName, true, transMapping);
		}else{
			rightExpression := self.map TransferSchemata2TypedNamedElementExpr(context, varName, transMapping);
		}endif;
		
		operator := LogicOperator::AND;
}

