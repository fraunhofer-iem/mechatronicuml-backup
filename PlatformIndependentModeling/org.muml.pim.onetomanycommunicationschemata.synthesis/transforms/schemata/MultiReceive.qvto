library MultiReceive;
import common.AllSchemataMappings;
modeltype ecore uses 'http://www.eclipse.org/emf/2002/Ecore';
modeltype rtsc uses pim::realtimestatechart('http://www.muml.org/pim/1.0.0');
modeltype common uses core::expressions::common('http://www.muml.org/core/1.0.0');
modeltype expressions uses core::expressions('http://www.muml.org/core/1.0.0');
modeltype behavior uses pim::behavior('http://www.muml.org/pim/1.0.0');
modeltype actionLanguage uses 'http://www.muml.org/pim/actionlanguage/1.0.0';
modeltype valuetype uses pim::valuetype('http://www.muml.org/pim/1.0.0');
modeltype protocol uses pim::protocol('http://www.muml.org/pim/1.0.0');
modeltype storydriven uses 'http://www.muml.org/core/1.0.0';
modeltype schemata uses pim::realtimestatechart::one_to_n_schemata('http://www.muml.org/pim/1.0.0');
modeltype types uses pim::types('http://www.muml.org/pim/1.0.0');
----------------------------------------------------------XXXXXXXXXXXXXXXX--MultiReceive--XXXXXXXXXXXXXXXXXX-------------------------------------------------------------------------
property multiReceiceChannelName : String = "multiReceive";
property multiReceiveDoneChannelName : String = "multiReceiveDone";

-------------------------------------------------------------Transfer Schema MultiReceive of Coordinator------------------------------------------------------------------

// @author: sthiele2 -- creates a transition in the coordinator region from the schema-transition's soruce to the intermediate state
mapping Transition :: MultiReceive2TransitionFromSourceToIntermediateCoordinator(context : OclAny, region:RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema()}{
	
		priority := self.priority;
		
		source := self.source.map Vertex2VertexCoordinator(context);
		
		
		target := self.map Schema2CoordinatorIntermediateState(context, 1,multiReceiveDLCName);
		
		guard := self.guard.map transform(result).oclAsType(Expression);
		
		if(not self.clockConstraints->select(c|not isClockSubRoleSpecific(c.clock))->isEmpty()) {
			urgent := false;
		}
		else {
			urgent := self.urgent;
		};	
		clockConstraints += self.clockConstraints->select(c|not isClockSubRoleSpecific(c.clock))->map transform(result).oclAsType(ClockConstraint);	
	
		synchronization := self. createSynchronization( multiReceiceChannelName,SynchronizationKind::SEND  ,PositionSelectorKind::FIRST);
		
}

// @author: sthiele2 -- create a transition in the coordinator region from the intermediate state to the schema-transition's target 
mapping Transition :: MultiReceive2TransitionFromIntermediateToTargetCoordinator(context : OclAny, region:RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema()} {
		
		priority := 1;
		
		source := self.map Schema2CoordinatorIntermediateState(context,1,multiReceiveDLCName);
		
		target := self.target.map Vertex2VertexCoordinator(context);
				
		clockResets := self.clockResets->select(c| not isClockSubRoleSpecific(c)).map transform().oclAsType(Clock);
		
		action := self.action.map transform(result).oclAsType(Action);
		
		if(not self.relativeDeadline.oclIsUndefined()){
			urgent := false;
			clockConstraints += self. RelativeDeadline2ClockConstraint(true, ComparingOperator::GREATER_OR_EQUAL, multicastDLCName);
		}endif;		
		
		synchronization := self. createSynchronization( multiReceiveDoneChannelName,SynchronizationKind::RECEIVE);
}

	
------------------------------------------------------------------ Transfer Schema MultiReceive of Subrole---------------------------------------------------------------

// @author: sthiele2 -- creates a transition in the subrole region from the schema-transition's source to the intermediate state 1
mapping Transition :: MultiReceive2TransitionFromSourceToIntermediateState1Subrole(context : RealtimeStatechart, region:RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema()} {
	
		priority := 2;
		
		source := self.map Transition2SourceStateSubrole(region);
		
		target := self.map Schema2SubroleIntermediateState(context, 1);
		
		if(not self.triggerMessageEvent.oneToManyCommunicationSchema.condition.oclIsUndefined())
		{
			guard := self.triggerMessageEvent.oneToManyCommunicationSchema.condition.map transform(result).oclAsType(Expression);
		}endif;
			
		synchronization := self. createSynchronization( multiReceiceChannelName,SynchronizationKind::RECEIVE, PositionSelectorKind::SELF);
}

// @author: sthiele2 -- creates a transition in the subrole region from the schema-transition's source to the intermediate state 2
mapping Transition :: MultiReceive2TransitionFromSourceToIntermediate2Subrole(context : RealtimeStatechart, region:RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema() and not self.triggerMessageEvent.oneToManyCommunicationSchema.condition.oclIsUndefined()}{
		
		priority := 1;
		
		source := self.map Transition2SourceStateSubrole(region);
		
		target := self.map Schema2SubroleIntermediateState(context, 2);		
		
		guard := self.triggerMessageEvent.oneToManyCommunicationSchema.condition.map Expression2UnaryExpression(result,UnaryOperator::NOT);		
		
		synchronization := self. createSynchronization( multiReceiceChannelName,SynchronizationKind::RECEIVE, PositionSelectorKind::SELF);
}


// @author: sthiele2 -- creates a transition in the subrole region from the intermediate state 1 source to the intermediate state 2 
// 					 -- implements the success case
mapping Transition :: MultiReceive2Transition1FromIntermediate1ToIntermediate2Subrole(context : RealtimeStatechart, region:RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema()} {
		
		priority := 1;
		
		source := self.map Schema2SubroleIntermediateState(context,1);
		
		target := self.map Schema2SubroleIntermediateState(context,2);
		
		events += self.triggerMessageEvent.map transform(result).oclAsType(AsynchronousMessageEvent);
		
		action := self.triggerMessageEvent.oneToManyCommunicationSchema.action.map transform(result).oclAsType(Action);
}

// @author: sthiele2 -- creates a second transition in the subrole region from the intermediate state 1 source to the intermediate state 2
//					 -- implements the failuer case
mapping Transition :: MultiReceive2Transition2FromIntermediate1ToIntermediate2Subrole(context : RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema()} {
		
		priority := 2;
		
		source := self.map Schema2SubroleIntermediateState(context,1);
		
		target := self.map Schema2SubroleIntermediateState(context,2);
		
		action := self.triggerMessageEvent.oneToManyCommunicationSchema.oclAsType(pim::realtimestatechart::one_to_n_schemata::MultiReceive).failureAction.map transform(result).oclAsType(Action);	
}	

// @author: sthiele2 -- creates a transition in the subrole region from the intermediate state 2 to the schema-transition's target state
// 					 -- implements the case that self == last
mapping Transition :: MultiReceive2Transition1FromIntermediate2ToTarget(context : RealtimeStatechart,region: RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema()} {
	
		priority := 1;
		
		source := self.map Schema2SubroleIntermediateState(context,2);
		
		target := self.map Transition2SourceStateSubrole(region);
		
		guard := createComparisonExpressionForSelectorKind(PositionSelectorKind::LAST, ComparingOperator::EQUAL);
	
		synchronization := 	self. createSynchronization( multiReceiveDoneChannelName,SynchronizationKind::SEND);
}

// @author: sthiele2 -- creates a transition in the subrole region from the intermediate state 2 to the schema-transition's target state
// 					 -- implements the case that self <> last
mapping Transition :: MultiReceive2Transition2FromIntermediate2ToTarget(context : RealtimeStatechart, region:RealtimeStatechart) : Transition
	when {self.usesMultiReceiveSchema()} {
	
		priority := 2;
		
		source := self.map Schema2SubroleIntermediateState(context,2);
		
		target := self.map Transition2SourceStateSubrole(region);
		
		guard := createComparisonExpressionForSelectorKind(PositionSelectorKind::LAST, ComparingOperator::UNEQUAL);
			
		synchronization := self. createSynchronization( multiReceiceChannelName,SynchronizationKind::SEND, PositionSelectorKind::SELF, PositionSelectorKind::NEXT);
}

