import transforms.lib.MUML2MUML;

modeltype muml_protocol uses muml::protocol('http://www.fujaba.de/muml/0.4.0');
modeltype rtsc uses muml::realtimestatechart('http://www.fujaba.de/muml/0.4.0');
modeltype mumlConstraint uses 'http://www.fujaba.de/muml/constraint/0.4.0';
modeltype mumlBehavior uses 'http://www.fujaba.de/muml/behavior/0.4.0';
modeltype mumlconnector uses muml::connector('http://www.fujaba.de/muml/0.4.0');
modeltype mumlmsgtype uses muml::msgtype('http://www.fujaba.de/muml/0.4.0');
modeltype modelinstance uses 'http://www.fujaba.de/modelinstance/0.4.0';
modeltype verification_extension uses 'http://www.fujaba.de/muml/verification/extension/0.4.0';


modeltype mtctl uses 'http://www.uni_paderborn.de/fujaba/muml/verification/uppaal/Mtctl';
modeltype mtctlQuantifiers uses mtctl::Quantifiers('http://www.uni_paderborn.de/fujaba/muml/verification/uppaal/Mtctl');
modeltype mtctlPredicates uses mtctl::Predicates('http://www.uni_paderborn.de/fujaba/muml/verification/uppaal/Mtctl');
modeltype mtctlSets uses mtctl::Sets('http://www.uni_paderborn.de/fujaba/muml/verification/uppaal/Mtctl');
modeltype mtctlBooleanLogic uses mtctl::BooleanLogic('http://www.uni_paderborn.de/fujaba/muml/verification/uppaal/Mtctl');
modeltype mtctlComparables uses mtctl::Comparables('http://www.uni_paderborn.de/fujaba/muml/verification/uppaal/Mtctl');
modeltype ecore uses 'http://www.eclipse.org/emf/2002/Ecore';

//Splits properties into multiple properties that each are rooted in a TemporalQuantifierExpr (these sub-properties can be verified by Uppaal)
//Afterwards, only one property repository exists for each verifiable element, including all properties from the original repositories.
transformation MTCTLPropertySplitTransformation(in input:muml_protocol, out output:muml_protocol) extends library MUML2MUML;

main() {
	input.objects()[RootNode]->map RootNode2RootNode();
	output.objects()[VerifiableElement]->select(vr | vr.extension->exists(oclIsKindOf(ElementToVerifyExtension)))->forEach(verifiableElement) {
		var combinedRepo := object PropertyRepository {};
		combinedRepo.properties := verifiableElement.verificationConstraintRepositories->selectByKind(PropertyRepository).properties->asOrderedSet();
		verifiableElement.verificationConstraintRepositories->forEach(repo) {
			output.removeElement(repo);
		};
		verifiableElement.verificationConstraintRepositories := combinedRepo;
	}
}

mapping mtctl::PropertyRepository :: PropertyRepository2PropertyRepository() : mtctl::PropertyRepository {
	//Go through all indirectly contained TemporalQuantifierExpressions and add them as a new Property to the target model repository
	self.properties->forEach(_property) {
		_property.allSubobjectsOfKind(TemporalQuantifierExpr)->forEach(_tempQuant) {
			properties += _tempQuant.oclAsType(TemporalQuantifierExpr).map TemporalQuantifierExpr2Property();
		};
	};
}

mapping TemporalQuantifierExpr::TemporalQuantifierExpr2Property() : mtctl::Property { //maps a TemporalQuantifierExpr onto a Property containing exactly that expression
	expression := self.map TemporalQuantifierExpr2TemporalQuantifierExpr();
	comment := self->closure(x|if (x.oclIsKindOf(Property)) then x else x.oclAsType(EObject).eContainer() endif)->any(oclIsKindOf(Property)).oclAsType(Property).comment;
}