transformation InstantiationTransformation(inout instance : InstancePackage);

modeltype MumlPackage uses muml('http://www.fujaba.de/muml/0.2.2');
modeltype ConnectorPackage uses muml::connector('http://www.fujaba.de/muml/0.2.2');
modeltype ValuetypePackage uses muml::valuetype('http://www.fujaba.de/muml/0.2.2');
modeltype ComponentPackage uses muml::component('http://www.fujaba.de/muml/0.2.2');
modeltype InstancePackage uses muml::instance('http://www.fujaba.de/muml/0.2.2');
modeltype Ecore uses ecore('http://www.eclipse.org/emf/2002/Ecore');

property NULL_COMPONENT_PART_HELPER : ComponentPart = object ComponentPart{};

main() {
	log("QVT-O Script \"instances.qvto\" started.");

	// process AtomicComponentInstances
	instance.rootObjects()[AtomicComponentInstance]->forEach(ci) {
		ci.portInstances := ci.componentType.createPortInstances(NULL_COMPONENT_PART_HELPER); // using helper instead of "null", because of QVTO-Bug
	};
	
	// process StructuredComponentInstances
	instance.rootObjects()[StructuredComponentInstance]->forEach(ci) {
		// Remove old port instances from resource
		ci.portInstances->forEach(pi) {
			instance.removeElement(pi);
		};
		ci.portInstances := ci.componentType.createPortInstances(NULL_COMPONENT_PART_HELPER); // using helper instead of "null", because of QVTO-Bug
		
		// Remove old embedded cic from resource
		instance.removeElement(ci.embeddedCIC);
		ci.embeddedCIC := new ComponentInstanceConfiguration(ci.componentType.oclAsType(StructuredComponent));
		
		if (ci.container().oclIsKindOf(ComponentInstanceConfiguration)) then {
			// Remove old connector instances from resource
			ci.container().oclAsType(ComponentInstanceConfiguration).portConnectorInstances->forEach(pci) {
				instance.removeElement(pci);
			};
			ci.container().oclAsType(ComponentInstanceConfiguration).portConnectorInstances := Set { };
			
			// Add new connector instances
			ci.componentType[StructuredComponent].connectors->forEach(portConnector) {
				var endpoint1 : ConnectorEndpoint := portConnector.connectorEndpoints->first();
				var endpoint2 : ConnectorEndpoint := portConnector.connectorEndpoints->last();
				if (endpoint1 != endpoint2) then {
					endpoint1.resolveIn(ConnectorEndpoint::single, PortInstance)->forEach(endpointInstance1) {
						endpoint2.resolveIn(ConnectorEndpoint::single, PortInstance)->forEach(endpointInstance2) {
							ci.container().oclAsType(ComponentInstanceConfiguration).portConnectorInstances += portConnector.createConnectorInstance(
								Set { endpointInstance1, endpointInstance2 }
							)[PortConnectorInstance];
						};
					};
				} endif;
			};
		} endif;
		
	};
}

// Convenience query to cast a natural number as integer, using -1 as value for infinity.
query NaturalNumber::toInteger() : Integer {
	if (self.infinity) then {
		return -1;
	} endif;
	return self.value;
}

// Convenience query to get all Port Instances that have already been created for a port at a given ComponentInstance
query Port::portInstances(componentInstance : ComponentInstance) : Set(PortInstance) {
	return componentInstance.portInstances->select(i | i.type = self);
}


query Component::createPortInstances(part : ComponentPart) : Set(PortInstance) {
	var portInstances : Set(PortInstance);
	self.ports->forEach(port) {
		portInstances += port.createInstances(part)[PortInstance];
	};
	return portInstances;
}

query ComponentPart::createPortInstances() : Set(PortInstance) {
	var portInstances : Set(PortInstance);
	self.portParts->forEach(portPart) {
		portInstances += portPart.createInstances(self)[PortInstance];
	};
	return portInstances;
}

// Creating multiple Port Instances for multi ports and one Port Instance for single ports.
query ConnectorEndpoint::createInstances(part : ComponentPart) : Set(ConnectorEndpointInstance) {
	return Set { self.createInstance(part) }
}
query DirectedTypedPort::createInstances(part : ComponentPart) : Set(ConnectorEndpointInstance) {
	if (self.optional) then {
		return Set { };
 	} endif;
	return Set { self.createInstance(part) };
}
query DiscretePort::createInstances(part : ComponentPart) : Set(ConnectorEndpointInstance) {
	if (self.multiPort) then {
		var multiPortInstance : DiscreteMultiPortInstance := self.map multi(part);
		//return OrderedSet { multiPortInstance }
		return OrderedSet { multiPortInstance }[PortInstance]->union(multiPortInstance.subInteractionEndpointInstances[PortInstance])
	} endif;
	return self.createSubPortInstances(part);
}
query DiscretePort::createSubPortInstances(part : ComponentPart) : Set(PortInstance) {
	var subPortInstances : Set(PortInstance);
	var count : Integer := 0;
	while (count < self.cardinality.lowerBound.toInteger()) {
		var portInstance : PortInstance := self.createInstance(part).oclAsType(PortInstance);
		portInstance.name := self.name + (count + 1).toString();
		subPortInstances += portInstance;
		count := count + 1;
	};
	return subPortInstances
}

// Primitively creating and initializing one PortInstance by PortType
query ConnectorEndpoint::createInstance(part : ComponentPart) : ConnectorEndpointInstance {
	return self.map single(part);
}

// Connector instances
query Connector::otherEnd(thisEnd : ConnectorEndpoint) : ConnectorEndpoint {
	return self.connectorEndpoints->select(e | e != thisEnd)->first();
}
query Connector::createConnectorInstance(endpoints : Set(ConnectorEndpointInstance)) : ConnectorInstance {
	var connectorInstance : ConnectorInstance := self.primCreateConnectorInstance();
	connectorInstance.connectorEndpointInstances := endpoints;
	return connectorInstance;
}
query Connector::primCreateConnectorInstance() : ConnectorInstance {
	assert fatal (false) with log ("Tried to instantiate abstract class Connector.");
	return null;
}
query AssemblyConnector::primCreateConnectorInstance() : ConnectorInstance {
	return new AssemblyConnectorInstance(self);
}
query DelegationConnector::primCreateConnectorInstance() : ConnectorInstance {
	return new DelegationConnectorInstance(self);
}




// Constructors
constructor ComponentInstanceConfiguration::ComponentInstanceConfiguration(component : StructuredComponent) {
	name := component.name;
	
	component.embeddedComponentParts->select(p | p.componentType.oclIsKindOf(AtomicComponent))->forEach(part) {
		componentInstances += new AtomicComponentInstance(part.componentType.oclAsType(AtomicComponent), part);
	};
	component.embeddedComponentParts->select(p | p.componentType.oclIsKindOf(StructuredComponent))->forEach(part) {
		componentInstances += new StructuredComponentInstance(part.componentType.oclAsType(StructuredComponent), part);
	};

	portConnectorInstances := componentInstances.portInstances.portConnectorInstances;
}

// TODO: Same code as below
constructor AtomicComponentInstance::AtomicComponentInstance(type : AtomicComponent, part : ComponentPart) {
	componentType := type;
	name := type.name;
	
	if (part != NULL_COMPONENT_PART_HELPER) then {
		// Creating Port Instances also sets the opposite feature PortInstance.component
		portInstances := part.createPortInstances();	
	
		// Set ComponentPart
		componentPart := part;
	} else {
	
		// Creating Port Instances also sets the opposite feature PortInstance.component
		portInstances := componentType.createPortInstances(part);
	} endif;

}

// TODO: Same code as above
constructor StructuredComponentInstance::StructuredComponentInstance(type : StructuredComponent, part : ComponentPart) {
	componentType := type;
	name := type.name;
	
	if (part != NULL_COMPONENT_PART_HELPER) then {
		// Creating Port Instances also sets the opposite feature PortInstance.component
		portInstances := part.createPortInstances();	
	
		// Set ComponentPart
		componentPart := part;
	} else {
	
		// Creating Port Instances also sets the opposite feature PortInstance.component
		portInstances := componentType.createPortInstances(part);
	} endif;
	
	// Create ConnectorInstances
	if (container().oclIsKindOf(ComponentInstanceConfiguration)) then {
		componentType[StructuredComponent].connectors->forEach(portConnector) {
			var endpoint1 : ConnectorEndpoint := portConnector.connectorEndpoints->first();
			var endpoint2 : ConnectorEndpoint := portConnector.connectorEndpoints->last();
			if (endpoint1 != endpoint2) then {
				endpoint1.resolveIn(ConnectorEndpoint::single, PortInstance)->forEach(endpointInstance1) {
					endpoint2.resolveIn(ConnectorEndpoint::single, PortInstance)->forEach(endpointInstance2) {
						container().oclAsType(ComponentInstanceConfiguration).portConnectorInstances += portConnector.createConnectorInstance(
							Set { endpointInstance1, endpointInstance2 }
						)[PortConnectorInstance];
					};
				};
			} endif;
		};
	} endif;
	
	embeddedCIC := new ComponentInstanceConfiguration(type);
}


constructor AssemblyConnectorInstance::AssemblyConnectorInstance(assembly : AssemblyConnector) {
	type := assembly;
}
constructor DelegationConnectorInstance::DelegationConnectorInstance(delegation : DelegationConnector) {
	type := delegation;
	
}


mapping ConnectorEndpoint::single(part : ComponentPart) : ConnectorEndpointInstance {
	init {
		result := self.map internalSingle(part);
	}
}

mapping ConnectorEndpoint::internalSingle(part : ComponentPart) : ConnectorEndpointInstance
	disjuncts PortPart::internalSingle, DiscretePort::internalSingleDiscrete, ContinuousPort::internalSingleContinuous, HybridPort::internalSingleHybrid;

mapping PortPart::internalSingle(part : ComponentPart) : ConnectorEndpointInstance {
	init {
		result := self.portType.map internalSingle(part);
	}
}

mapping DiscretePort::multi(part : ComponentPart) : DiscreteMultiPortInstance {
	type := self;
	name := type.name;
	subInteractionEndpointInstances := self.createSubPortInstances(part)[DiscreteSinglePortInstance];
}
mapping DiscretePort::internalSingleDiscrete(part : ComponentPart) : DiscreteSinglePortInstance {
	type := self;
}
mapping ContinuousPort::internalSingleContinuous(part : ComponentPart) : ContinuousPortInstance {
	type := self;
}
mapping HybridPort::internalSingleHybrid(part : ComponentPart) : HybridPortInstance {
	type := self;
}